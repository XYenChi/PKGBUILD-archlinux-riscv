# Maintainer: XYenChi <oriachiuan@gmail.com>
pkgname=python-safetensors
pkgver=0.4.3
pkgrel=1
pkgdesc="Fast and safe tensor serialization for Python (Rust/PyO3)"
url="https://github.com/huggingface/safetensors"
license=('Apache')
arch=('riscv64')

depends=('python')
makedepends=(
  'rust'
  'cargo'
  'python-build'
  'python-installer'
  'python-wheel'
  'python-maturin'
  'python-setuptools-rust'
)

source=(
  "https://files.pythonhosted.org/packages/source/s/safetensors/safetensors-${pkgver}.tar.gz"
)

sha256sums=('SKIP')

build() {
  cd "$srcdir/safetensors-${pkgver}"

  export PYO3_PYTHON=/usr/bin/python
  export PYTHON_SYS_EXECUTABLE=/usr/bin/python
  export PYO3_USE_ABI3_FORWARD_COMPATIBILITY=1 # for python 3.13
  python -m build --wheel --no-isolation
}

package() {
  cd "$srcdir/safetensors-${pkgver}"

  # 安装刚刚构建的 wheel
  python -m installer --destdir="$pkgdir" dist/*.whl
}

